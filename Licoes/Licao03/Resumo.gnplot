# Arquivo de resumo da licao 3

# O proprio txt dessa liçao eh bem resumido, nao existe muito o que resumir. 
# Esse script simplesmente serve de base para caso se queira repetir as operaçoes
# realizadas na aula, mudando os parametros estilisticos.

# EXEMPLO DE SCRIPT ORGANIZADO:

# Informaçoes de Execuçao, terminal e output
#set terminal pngcairo size 1920,980
#set output "Script_Organizado.png"

## Informaçoes de layout
# Definir Legendas
set key # ativar a legenda de cada dataset (podemos desligar)
set key top right # Mudar a posiçao da legenda
set key width 1 # Criar uma espessura invisivel, um offset
set key box # Adicionar uma caixa na legenda
set key font ",14" # Alterar o tamanho da fonte somente
#set key at 0.3, -1.4 # Posicionar em relacao a coordenadas

# Definir o range que o grafico ira mostrar
set xrange [-0.5:20]
set yrange [-0.2:1.5]
# Definir a legenda dos eixos x e y
set xlabel "Tempo [s]"
set ylabel "Massa [Kg/s]"
# Definir a fonte dos eixos x e y
set xlabel font ",14"
set ylabel font ",16"
# Adicionar um offset para o label nao 
# se aproximar muito dos tics
set xlabel offset -1
set ylabel offset 3

# Definir bordas
set border 3 # Mantem apenas a borda inferior e esquerda
set border linewidth 2 # aumenta a espessura das bordas

# Definir o grid do grafico
set grid

# Definir Tics (opçoes adicionais comentadas)
set tics nomirror # Desativa os tics superior e direito
set tics scale 1.5 # Aumenta espessura dos tics
set tics font ",16" # Altera o tamanho da fonte somente
#set tics offset 3 # Adiciona um offset aos tics

## Informaçoes de plotagem (Nao sera escrito como linha unica)

# Definindo estilos de plotagem
set style line 1 \
linecolor rgb "#8b0000" \
linewidth 2

set style line 2 \
linecolor 0 \
pointtype 7 \
pointsize 0.9

# Definindo plotagem

f(x) = a*exp(-b*x)
fit f(x) "dados_py.dat"\
using 1:2 \
via a,b

g(x) = c*exp(-d*x)
fit g(x) "dados_py.dat"\
using 1:2:3 \
yerrors \
via c,d


plot \
"dados_py.dat" \
title "Pontos do CSV" \
linestyle 2\
with errorbars \
,\
f(x)\
linestyle 1 \
title "MMQ S/ Pesos"\
,\
g(x) \
title "MMQ C/ Pesos"\
